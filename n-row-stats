#!/bin/sh
set -euf

##
# Given a list of numbers, calculate many statistics.
#
# Example:
#
#     $ echo "1\n2\n4" | n-row-stats
#     n: 3 min: 1 max: 4 sum: 7 range: 3 median: 2 mean: 2.33333 sumsq: sd: 1.24722 cv: 0.534523
#
# Author: Joel Parker Henderson (joel@joelparkerhenderson.com)
# License: GPL
# Updated: 2015-09-25
# Version: 1.0.1
##

awk '{
  sum += $1
  a[NR] = $1
}
END{
  n = NR
  asort(a)
  min = a[1]
  max = a[n]
  mean = sum / n
  range = max - min

  if (n % 2) {
    median = a[(n + 1) / 2];
  } else {
    median = (a[(n / 2)] + a[(n / 2) + 1]) / 2.0;
  }

  for (i = 1; i <= n; i++){
    sumsq += (a[i] - mean) ** 2;
  }
  sd = sqrt(sumsq/n)
  cv = sd/mean

  print "n:", n, "min:", min, "max:", max, "sum:", sum, "range:", range, "median:", median, "mean:", mean, "sumsq:", sumsq, "sd:", sd, "cv:", cv
}'
